# 计算机启动过程

1. 执行ROM-BIOS

在处理器众多的引脚中，有一个是RESET，用于接受复位信号。每当处理器加电，或者RESET引脚的电平由低变高时，处理器都会执行一个硬件初始化，以及一个可选的内部自测试（Build-in Self-Test，BIST），然后将内部所有寄存器的内容初始到一个预置的状态。

比如，对于Intel 8086来说，复位将使代码段寄存器（CS）的内容为0xFFFF，其他所有寄存器的内容都为0x0000。在以Intel 8086为处理器的系统中，ROM 占据着整个内存空间顶端的64KB，物理地址范围是0xF0000～0xFFFFF，里面固化了开机时要执行的指令；DRAM占据着较低端的640KB，地址范围是0x00000～0x9FFFF；中间一部分，分给了其他外围设备。8086在形成物理地址时，先将段寄存器内容左移4位，形成20位段地址，再同16位的偏移地址相加。8086 加电或者复位后，CS=0xFFFF，IP=0x0000，所以，它取的第一条指令位于物理地址0xFFFF0，正好位于ROM 中，那里固化了开机时需要执行的指令。ROM 中位于物理地址0xFFFF0 的地方，通常是一个跳转指令，通过改变CS和IP的内容，使处理器从ROM 中的较低地址处开始取指令执行。在NASM 汇编语言里，一个典型的跳转指令像这样：

```assembly
jmp 0xf000:0xe05b
```

0xf000是要跳转到的段地址，用来改变CS寄存器的内容；0xe05b是目标代码段内的偏移地址，用来改变IP寄存器的内容。因此，目标位置的物理地址是0xfe05b。一旦执行这条指令，处理器将开始从指定的“段: 偏移”处开始重新取指令执行。

这块ROM 芯片主要是进行硬件的诊断、检测和初始化。它还负责提供一套软件例程，让人们在不必了解硬件细节的情况下从外围设备（比如键盘）获取输入数据，或者向外围设备（比如显示器）输出数据。最后，它会从辅助存储设备读取指令数据，然后转到那里开始执行。这块芯片也叫基本输入输出系统（Base Input & Output System，BIOS）ROM。

以下是8086处理器内部组成框图

<img src="/home/cccmmf/操作系统/x86/chap2/2023-03-05_22-42.png" alt="2023-03-05_22-42" style="zoom:75%;" />

其中CS为代码段寄存器，DS为数据段寄存器，ES为附加段寄存器，SS为栈段寄存器，IP为指令寄存器。AX，BX，CX，DX，SI，DI，BP，SP为16位通用寄存器，其中AX、BX、CX和DX可以分为2个8位寄存器使用。

2. 执行主引导扇区

硬盘的第一个扇区是0面0道1扇区，也叫主引导扇区，大小为512字节。如果计算机设置从硬盘启动，ROM-BIOS将主引导扇区加载到0x0000:0x7c00处，然后判断是否有效，有效的主引导扇区最后俩字节为0x55和0xAA。ROM-BIOS先检查这俩字节，如果有效就跳转到0x0000:0x7c00处继续执行。

```assembly
jmp 0x0000:7c00
```

主引导扇区将检测用来启动计算机的操作系统，并计算出它在硬盘位置。然后它把操作系统的自举代码加载到内存，然后用jmp指令跳转到那里开始执行，直到操作系统完全启动。

在32位的IA-32处理器下，主引导扇区可以负责构造一些段描述符，然后通过设置cr0的PE位，从实模式切换为保护模式，通过一条jmp指令清流水线并串行化处理器。然后将操作系统内核代码从硬盘加载到内存中，将操作系统内核加载完毕后，为操作系统内核创建一些段描述符，最终跳转到内核代码去执行。

以下一段代码是将硬盘中的代码和数据加载到内存中

```assembly
read_hard_disk_0:                        ;从硬盘读取一个逻辑扇区
                                         ;输入：DI:SI=起始逻辑扇区号,DI中只有低12位有效，高4位置0，因为逻辑扇区号是28位
                                         ;DS:BX=目标缓冲区地址
         push ax
         push bx
         push cx
         push dx
         ;向0x1f端口写入要读取的扇区数
         mov dx,0x1f2         ;向0x1f2端口写入要读取的扇区数量
         mov al,1
         out dx,al                       ;读取的扇区数
         ;向硬盘接口写入起始逻辑扇区号的低24位
         inc dx                          ;0x1f3
         mov ax,si
         out dx,al                       ;LBA地址7~0

         inc dx                          ;0x1f4
         mov al,ah
         out dx,al                       ;LBA地址15~8

         inc dx                          ;0x1f5
         mov ax,di
         out dx,al                       ;LBA地址23~16
         ;向硬盘接口写入27~24位和工作模式（LBA）
         inc dx                          ;0x1f6
         mov al,0xe0                     ;LBA28模式，主盘，27-25位111表示LBA模式，24位为0表示读主硬盘，为1表示读从硬盘
         or al,ah                        ;LBA地址27~24
         out dx,al
         ;向硬盘接口发送读命令
         inc dx                          ;0x1f7，命令端口
         mov al,0x20                     ;读命令，0x20为读，0x30为写
         out dx,al

  .waits:
         in al,dx
         and al,0x88          ;端口0x1f7既是命令端口，又是状态端口。0x1f7端口第七位置1，表示自己忙。一旦硬盘准备就绪，此位清零。第三位置1表示自己准备好了，请求主机发送或者接收数据。
         cmp al,0x08
         jnz .waits                      ;不忙，且硬盘已准备好数据传输 
         
         mov cx,256                      ;总共要读取的字数
         mov dx,0x1f0         ;0x1f0为16位的端口，硬盘接口的数据端口
  .readw:
         in ax,dx
         mov [bx],ax          ;将读出来的数据放到ds指向的数据段，起始偏移地址在bx中。
         add bx,2
         loop .readw

         pop dx
         pop cx
         pop bx
         pop ax
      
         ret
```

3. 当执行完引导扇区代码将操作系统内核加载到内存后，就会进入操作系统内核开始执行。

